# MODEL 2 

# Ras-SOS Activation Model 2 
# altered parameters for multimodality 
# increased Kcat2 and decreased Koff1 


begin model

begin parameters
    RhoSOS 1              # nM # initial RhoSOS parameter for scanning
    
    Kon1 7e-8* RhoSOS           # sos binding to RasGDP 
    Kon2 7e-8* RhoSOS             # sos binding to RasGTP 
    
    Koff1 1e-4            # Dissociation rate for RasGDP·SOS  # initial Koff1 parameter in Model 1 = 5e-3 
    Koff2 5e-4            # Dissociation rate for RasGTP·SOS 
    
    Kcat1 1e-2 / A        # Catalytic rate for RasGDP -> RasGTP
    Kcat2 2.5e-2          # Catalytic rate for RasGTP -> RasGDP.  # initial Kcat2 parameter in Model 2 = 2.5e-3 
    
    A 1                    # Area, (µm²)
    RasTotal 1000 #* A          # Total number of Ras molecules, initially all RasGDP
    
    initialBoundSOS 1
    
end parameters

begin molecule types
    Ras(sos,a~p~0)        
    Sos(ras)             
end molecule types

begin species
    Ras(sos,a~0) RasTotal    # Initial RasGDP molecules
    Ras(sos!1,a~0).Sos(ras!1) initialBoundSOS
end species

begin observables
    Molecules boundRasGTP Ras(sos!1,a~p).Sos(ras!1)
    Molecules boundRasGDP Ras(sos!1,a~0).Sos(ras!1)
    Molecules totalBoundSOS Ras(sos!1).Sos(ras!1) # bound Ras-SOS complexes 
    Molecules unboundRasGTP Ras(sos,a~p)
    Molecules unboundRasGDP Ras(sos,a~0)
    Molecules totalRasGTP Ras(a~p)
    Molecules TotalRas Ras()
end observables

begin functions 
    activatedRasGTP() totalRasGTP / TotalRas
end functions

begin reaction rules
    RasPhos:        Ras(sos,a~0) + Ras(sos!1).Sos(ras!1) -> Ras(sos,a~p) + Ras(sos!1).Sos(ras!1) Kcat1
    RasDephos:      Ras(sos,a~p) -> Ras(sos,a~0) Kcat2

    SosGDPbinding:  Ras(sos,a~0) <-> Ras(sos!1,a~0).Sos(ras!1) Kon1,Koff1
    SosGTPbinding:  Ras(sos,a~p) <-> Ras(sos!1,a~p).Sos(ras!1) Kon2,Koff2
end reaction rules

end model


simulate({method=>"ssa", t_end=>1000000, n_steps=>100000, print_functions=>1})


# parameter_scan({method=>"ssa",parameter=>"RhoSOS",par_min=>1,par_max=>10,\
#                n_scan_pts=>100,t_end=>10000,n_steps=>2})


